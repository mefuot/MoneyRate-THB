plugins {
    id "org.sonarqube" version "2.3"
}
apply plugin: 'com.android.application'
apply from: '../jacoco.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "com.pong.moneyrate"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
//            shrinkResources true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
//            testCoverageEnabled true
        }
    }
    productFlavors {
        mock {
            resConfigs "en","th","xhdpi"
        }
        prod {
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.contains('mock')) {
            variant.setIgnore(true);
        }
    }

    if(project.hasProperty('devBuild')){
        splits.abi.enable = false
        splits.density.enable = false
        aaptOptions.cruncherEnabled = false
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'org.parceler:parceler-api:1.1.6'
    annotationProcessor 'org.parceler:parceler:1.1.6'

    compile 'com.ogaclejapan.smarttablayout:library:1.6.1'

    compile 'com.pong.library:mvp-structure:1.0.0@aar'
    compile 'com.pong.library:multi-layout:0.9.0'


    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-android:2.7.22'
}

// run jacoco       -> ./gradlew jacocoTestReport
// run sonarQube    -> ./gradlew sonarqube

sonarqube {
    properties {
        property "sonar.projectName", "MoneyRate"
        property "sonar.projectKey", "com.pong.moneyrate"
        property "sonar.sources", "src/main"

        property "sonar.tests", "src/test"
        property "sonar.java.binaries", "build/intermediates/classes/mock/debug"
        property "sonar.jacoco.reportPaths","build/jacoco/testMockDebugUnitTest.exec"
    }
}
